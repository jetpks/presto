#
# A colorful, friendly, multiline theme with some handy features.
#
# Authors:
#   Paul Gideon Dann <pdgiddie@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Simple VCS branch, staged, and unstaged indication.
#   - Prompt character is different in a VCS repository.
#   - Last command exit status is displayed when non-zero.
#
# Screenshots:
#   http://i.imgur.com/rCo3S.png
#

function +vi-git_precmd {
  # Check for untracked files, since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]+='%F{green}?%f'
  fi
}

function prompt_giddie_precmd {
  _prompt_re5et_time="$(dt=$(date '+%Y%b%e/%k.%M.%S');echo $dt:l)"
  vcs_info
}

function prompt_giddie_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_giddie_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{red}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{green}--- COMMAND ---%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' get-revision true
  zstyle ':vcs_info:*' formats ' on %F{magenta}%b%f^%8.8i %c%u'
  zstyle ':vcs_info:*' actionformats ' on %F{magenta}%b%f^%8.8i %c%u %F{red}(%B%a%%b)%f'
  zstyle ':vcs_info:*' stagedstr '%F{green}±%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red}±%f'

  # Define prompts.
  PROMPT='%B%F{blue}%n%f%b%F{magenta}@%f%B%F{blue}%m%f%b%F{magenta}:%f%F{blue}%~%f${vcs_info_msg_0_}
%F{magenta}%%%f '
  RPROMPT='%(?..%F{magenta}%B:( %?  %b%f)%F{magenta}${_prompt_re5et_time}%f'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_giddie_setup "$@"
